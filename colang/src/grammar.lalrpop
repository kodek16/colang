use crate::ast::*;

grammar(file: InputSpanFile);

pub Program: Program = {
    <program:Program?> <struct_:StructDef> => {
        let mut program = program.unwrap_or(Program::new());
        program.structs.push(struct_);
        program
    },
    <program:Program?> <function:FunctionDef> => {
        let mut program = program.unwrap_or(Program::new());
        program.functions.push(function);
        program
    },
};

FunctionDef: FunctionDef = {
    <start:@L> KwFun <name:Ident> "(" <parameters:CommaSeparated<ParameterDef>> ")" <return_type:(":" <TypeExpr>)?> <end:@R> <body:Block> => {
        FunctionDef {
            name,
            parameters,
            return_type,
            body,
            signature_span: InputSpan { file, start, end }
        }
    },
};

ParameterDef: Parameter = {
    NormalParameterDef,
    SelfParameterDef,
};

NormalParameterDef: Parameter = {
    <start: @L> <name:Ident> ":" <type_:TypeExpr> <end: @R> => {
        Parameter::Normal(NormalParameter {
            name,
            type_,
            span: InputSpan { file, start, end },
        })
    },
};

SelfParameterDef: Parameter = {
    <start:@L> KwSelf <end:@R> => {
        Parameter::Self_(SelfParameter {
            kind: SelfParameterKind::ByValue,
            span: InputSpan { file, start, end },
        })
    },

    <start:@L> "&" KwSelf <end:@R> => {
        Parameter::Self_(SelfParameter {
            kind: SelfParameterKind::ByPointer,
            span: InputSpan { file, start, end },
        })
    },
};

StructDef: StructDef = {
    <start:@L> KwStruct <name:Ident> <end:@R> "{" <members:StructMember*> "}" => {
        StructDef::new(
            name,
            members,
            InputSpan { file, start, end },
        )
    },
};

StructMember: StructMember = {
    FieldDef,
    MethodDef,
};

FieldDef: StructMember = {
    <start:@L> <name:Ident> ":" <type_:TypeExpr> <end:@R> ";" => {
        StructMember::Field(FieldDef {
            name,
            type_,
            span: InputSpan { file, start, end },
        })
    },
};

MethodDef: StructMember = {
    FunctionDef => StructMember::Method(<>),
};

Stmt: Statement = {
    StmtNotExpr,

    <start: @L> <expression:ExprWithBlock> <end: @R> => {
        Statement::Expr(ExprStmt {
            expression,
            span: InputSpan { file, start, end },
        })
    },
}

// Somewhat ambiguously named, StmtNotExpr is a statement that was not trivially converted
// from an expression ending with block. If a semicolon was added to an expression, it can
// also be a StmtNotExpr.
StmtNotExpr: Statement = {
    <start: @L> KwVar <entries:CommaSeparatedNotEmpty<VarDeclEntry>> ";" <end: @R> => {
        Statement::VarDecl(VarDeclStmt {
            entries,
            span: InputSpan { file, start, end },
        })
    },

    <start: @L> KwRead <entries:CommaSeparatedNotEmpty<ReadEntry>> ";" <end: @R> => {
        Statement::Read(ReadStmt {
            entries,
            span: InputSpan { file, start, end },
        })
    },

    <start: @L> KwWrite <expression:Expr> ";" <end: @R> => {
        Statement::Write(WriteStmt {
            expression,
            span: InputSpan { file, start, end },
        })
    },

    <start: @L> KwWhile <cond:Expr> <body:Block> <end: @R> => {
        Statement::While(WhileStmt {
            cond: Box::new(cond),
            body: Box::new(body),
            span: InputSpan { file, start, end },
        })
    },

    <start: @L> <lhs:Expr> "=" <rhs:Expr> ";" <end: @R> => {
        Statement::Assign(AssignStmt {
            lhs: Box::new(lhs),
            rhs: Box::new(rhs),
            span: InputSpan { file, start, end },
        })
    },

    <start: @L> <expression:Expr> ";" <end: @R> => {
        Statement::Expr(ExprStmt {
            expression,
            span: InputSpan { file, start, end },
        })
    },
};

VarDeclEntry: VarDeclEntry = {
    <start: @L> <name: Ident> <type_:(":" <TypeExpr>)?> <initializer:("=" <Expr>)?> <end: @R> => {
        VarDeclEntry {
            variable_name: name,
            variable_type: type_,
            initializer,
            span: InputSpan { file, start, end },
        }
    }
};

ReadEntry: ReadEntry = {
    <start:@L> <target:Expr> <end:@R> => {
        ReadEntry {
            target,
            span: InputSpan { file, start, end },
        }
    },
};

// Expressions are defined from lowest to highest precedence (Expr0 is highest).
// The top-level non-terminal structure handles the dangling-else problem.
// (https://en.wikipedia.org/wiki/Dangling_else).
//
// Duplicated XExpr/XExprWithBlock non-terminals are there to make en exception
// and allow expressions that end in a block to be converted to statements without
// a terminating semicolon.
Expr: Expression = {
    OpenIfExpr,
    ClosedExpr,
};

ExprWithBlock: Expression = {
    BlockExpr,
    IfExprWithBlock,
};

IfExprWithBlock: Expression = {
    OpenIfExprWithBlock,
    ClosedIfExprWithBlock,
};

OpenIfExpr: Expression = {
    <start: @L> KwIf "(" <cond:Expr> ")" <then:Expr> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: None,
            span: InputSpan { file, start, end },
        })
    },
    <start: @L> KwIf "(" <cond:Expr> ")" <then:ClosedExpr> KwElse <else_:OpenIfExpr> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: Some(Box::new(else_)),
            span: InputSpan { file, start, end },
        })
    },
};

OpenIfExprWithBlock: Expression = {
    <start: @L> KwIf "(" <cond:Expr> ")" <then:ExprWithBlock> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: None,
            span: InputSpan { file, start, end },
        })
    },
    <start: @L> KwIf "(" <cond:Expr> ")" <then:ClosedExpr> KwElse <else_:OpenIfExprWithBlock> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: Some(Box::new(else_)),
            span: InputSpan { file, start, end },
        })
    },
};

ClosedExpr: Expression = {
    NonIfExpr,
    ClosedIfExpr,
}

ClosedExprWithBlock: Expression = {
    NonIfExprWithBlock,
    ClosedIfExprWithBlock,
};

ClosedIfExpr: Expression = {
    <start: @L> KwIf "(" <cond:Expr> ")" <then:ClosedExpr> KwElse <else_:ClosedExpr> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: Some(Box::new(else_)),
            span: InputSpan { file, start, end },
        })
    },
};

ClosedIfExprWithBlock: Expression = {
    <start: @L> KwIf "(" <cond:Expr> ")" <then:ClosedExpr> KwElse <else_:ClosedExprWithBlock> <end: @R> => {
        Expression::If(IfExpr {
            cond: Box::new(cond),
            then: Box::new(then),
            else_: Some(Box::new(else_)),
            span: InputSpan { file, start, end },
        })
    },
};

NonIfExpr: Expression = {
    BinaryOpExpr,
    BlockExpr,
};

NonIfExprWithBlock: Expression = {
    BlockExpr,
};

BlockExpr: Expression = {
    Block => Expression::Block(<>),
};

// Lowest precedence level.
BinaryOpExpr = Expr5;

Expr5 = LeftAssocBinaryOpExprTier<Expr5Op, Expr4>;
Expr5Op: BinaryOperator = {
    "==" => BinaryOperator::Eq,
    "!=" => BinaryOperator::NotEq,
}

Expr4 = LeftAssocBinaryOpExprTier<Expr4Op, Expr3>;
Expr4Op: BinaryOperator = {
    "<" => BinaryOperator::Less,
    ">" => BinaryOperator::Greater,
    "<=" => BinaryOperator::LessEq,
    ">=" => BinaryOperator::GreaterEq,
}

Expr3 = LeftAssocBinaryOpExprTier<Expr3Op, Expr2>;
Expr3Op: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
}

Expr2 = LeftAssocBinaryOpExprTier<Expr2Op, Expr1>;
Expr2Op: BinaryOperator = {
    "*" => BinaryOperator::Mul,
}

Expr1: Expression = {
    AddressExpr,
    DerefExpr,
    Expr0,
};

AddressExpr: Expression = {
    <start:@L> "&" <target:Expr0> <end:@R> => {
        Expression::Address(AddressExpr {
            target: Box::new(target),
            span: InputSpan { file, start, end },
        })
    },
};

DerefExpr: Expression = {
    <start:@L> "*" <pointer:Expr0> <end:@R> => {
        Expression::Deref(DerefExpr {
            pointer: Box::new(pointer),
            span: InputSpan { file, start, end },
        })
    },
};

Expr0: Expression = {
    VariableExpr,
    IntLiteralExpr,
    BoolLiteralExpr,
    CharLiteralExpr,
    StringLiteralExpr,
    SelfExpr,
    NewExpr,
    ArrayFromElementsExpr,
    ArrayFromCopyExpr,
    IndexExpr,
    CallExpr,
    FieldAccessExpr,
    MethodCallExpr,
    ParensExpr,
};

VariableExpr: Expression = {
    <start: @L> <name:Ident> <end: @R> => {
        Expression::Variable(VariableExpr {
            name,
            span: InputSpan { file, start, end },
        })
    },
};

IntLiteralExpr: Expression = {
    <start: @L> <value:IntLiteral> <end: @R> => {
        Expression::IntLiteral(IntLiteralExpr {
            value,
            span: InputSpan { file, start, end },
        })
    },
};

BoolLiteralExpr: Expression = {
    <start: @L> <value:BoolLiteral> <end: @R> => {
        Expression::BoolLiteral(BoolLiteralExpr {
            value,
            span: InputSpan { file, start, end },
        })
    },
};

CharLiteralExpr: Expression = {
    <start:@L> <value:CharLiteral> <end:@R> => {
        Expression::CharLiteral(CharLiteralExpr {
            value,
            span: InputSpan { file, start, end },
        })
    },
};

StringLiteralExpr: Expression = {
    <start:@L> <value:StringLiteral> <end:@R> => {
        Expression::StringLiteral(StringLiteralExpr {
            value,
            span: InputSpan { file, start, end },
        })
    },
};

SelfExpr: Expression = {
    <start:@L> KwSelf <end:@R> => {
        Expression::Self_(SelfExpr {
            span: InputSpan { file, start, end },
        })
    },
};

NewExpr: Expression = {
    <start:@L> KwNew <target_type:TypeExpr> <end:@R> => {
        Expression::New(NewExpr {
            target_type,
            span: InputSpan { file, start, end },
        })
    },
};

ArrayFromElementsExpr: Expression = {
    <start:@L> "[" <elements:CommaSeparated<Expr>> "]" <end:@R> => {
        Expression::ArrayFromElements(ArrayFromElementsExpr {
            elements,
            span: InputSpan { file, start, end },
        })
    },
};

ArrayFromCopyExpr: Expression = {
    <start:@L> "[" <element:Expr> ";" <size:Expr> "]" <end:@R> => {
        Expression::ArrayFromCopy(ArrayFromCopyExpr {
            element: Box::new(element),
            size: Box::new(size),
            span: InputSpan { file, start, end },
        })
    },
};

IndexExpr: Expression = {
    <start:@L> <collection:Expr0> "[" <index:Expr> "]" <end:@R> => {
        Expression::Index(IndexExpr {
            collection: Box::new(collection),
            index: Box::new(index),
            span: InputSpan { file, start, end },
        })
    },
};

CallExpr: Expression = {
    <start: @L> <function_name:Ident> "(" <arguments:CommaSeparated<Expr>> ")" <end: @R> => {
        Expression::Call(CallExpr {
            function_name,
            arguments,
            span: InputSpan { file, start, end },
        })
    },
};

FieldAccessExpr: Expression = {
    <start:@L> <receiver:Expr0> "." <field:Ident> <end:@R> => {
        Expression::FieldAccess(FieldAccessExpr {
            receiver: Box::new(receiver),
            field,
            span: InputSpan { file, start, end },
        })
    },
};

MethodCallExpr: Expression = {
    <start:@L> <receiver:Expr0> "." <method:Ident> "(" <arguments:CommaSeparated<Expr>> ")" <end:@R> => {
        Expression::MethodCall(MethodCallExpr {
            receiver: Box::new(receiver),
            method,
            arguments,
            span: InputSpan { file, start, end },
        })
    },
};

ParensExpr: Expression = {
    <start: @L> "(" <e:Expr> ")" <end: @R> => {
        e.map_span(|_| InputSpan { file, start, end })
    },
};

Block: BlockExpr = {
    <start: @L> "{" "}" <end: @R> => {
        BlockExpr {
            statements: vec![],
            final_expr: None,
            span: InputSpan { file, start, end },
        }
    },

    <start: @L> "{" <statements:(<Stmt>)*> <final_stmt:StmtNotExpr> "}" <end: @R> => {
        let mut statements = statements;
        statements.push(final_stmt);
        BlockExpr {
            statements,
            final_expr: None,
            span: InputSpan { file, start, end },
        }
    },

    <start: @L> "{" <statements:(<Stmt>)*> <final_expr:Expr> "}" <end: @R> => {
        BlockExpr {
            statements,
            final_expr: Some(Box::new(final_expr)),
            span: InputSpan { file, start, end },
        }
    },
};

TypeExpr: TypeExpr = {
    ScalarTypeExpr,
    ArrayTypeExpr,
    PointerTypeExpr,
};

ScalarTypeExpr: TypeExpr = {
    <start:@L> <name:Ident> <end:@R> => {
        TypeExpr::Scalar(ScalarTypeExpr {
            name,
            span: InputSpan { file, start, end },
        })
    },
};

ArrayTypeExpr: TypeExpr = {
    <start:@L> "[" <element:TypeExpr> "]" <end:@R> => {
        TypeExpr::Array(ArrayTypeExpr {
            element: Box::new(element),
            span: InputSpan { file, start, end },
        })
    },
};

PointerTypeExpr: TypeExpr = {
    <start:@L> "&" <target:TypeExpr> <end:@R> => {
        TypeExpr::Pointer(PointerTypeExpr {
            target: Box::new(target),
            span: InputSpan { file, start, end },
        })
    },
};

Ident: Identifier = {
    <start: @L> <text:r"[_a-zA-Z][_a-zA-Z0-9]*"> <end: @R> => Identifier {
        text: String::from(text),
        span: InputSpan { file, start, end },
    }
};

IntLiteral: i32 = {
    r"-?[0-9]+" => <>.parse().unwrap(),
};

BoolLiteral: bool = {
    KwTrue => true,
    KwFalse => false,
}

CharLiteral: String = {
    <literal:r#"'([^'\\\n\r\t]|\\.)*'"#> => String::from(&literal[1..literal.len()-1]),
};

StringLiteral: String = {
    <literal:r#""([^"\\\n\r\t]|\\.)*""#> => String::from(&literal[1..literal.len()-1]),
};

// Keywords:
KwVar: () = { "var" }
KwRead: () = { "read" }
KwWrite: () = { "write" }
KwIf: () = { "if" }
KwElse: () = { "else" }
KwWhile: () = { "while" }
KwFun: () = { "fun" }
KwTrue: () = { "true" }
KwFalse: () = { "false" }
KwStruct: () = { "struct" }
KwSelf: () = { "self" }
KwNew: () = { "new" }

// Macros:
CommaSeparatedNotEmpty<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T> => {
        let mut v = v;
        v.push(e);
        v
    }
};

CommaSeparated<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

LeftAssocBinaryOpExprTier<Ops, PrevTier>: Expression = {
    <start: @L> <lhs:LeftAssocBinaryOpExprTier<Ops, PrevTier>> <operator:Ops> <rhs:PrevTier> <end: @R> => {
        Expression::BinaryOp(BinaryOperatorExpr {
            operator,
            lhs: Box::new(lhs),
            rhs: Box::new(rhs),
            span: InputSpan { file, start, end },
        })
    },
    PrevTier,
}

// Lexer:

match {
    // Ignore line comments.
    r"//[^\n\r]*[\n\r]*" => { },
} else {
    // Ignore whitespace.
    r"\s*" => { },
    _ // Everything else
}
